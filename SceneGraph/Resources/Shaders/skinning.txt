
#ifdef SKIN_POSITIONS

void skinPositions(){

	vec4 transformedPosition = vec4(0.0,0.0,0.0,0.0);
	
	mat3 curIndex = a_index;
	mat3 curWeight = a_weight;
	
	for (int y = 0; y < 3; y++){
		for( int x = 0; x < 3; x++ ){
			float weight = curWeight[x][y];
			if(weight > 0.0){
				int idx = int(curIndex[x][y]);
				
				// transform the offset by bone i
				mat4 m44 = u_boneMatrices[idx];
				transformedPosition += m44 * a_position * weight;
			}
		}
	}
	
	gl_Position = u_modelViewProjectionMatrix * transformedPosition;    
	
}

#else
#ifdef SKIN_POSITIONS_AND_NORMALS

void skinPositionsAndNormals(){
	vec4 transformedPosition = vec4(0.0,0.0,0.0,0.0);
	vec4 transformedNormal = vec4(0.0,0.0,0.0,0.0);
	
	mat3 curIndex = a_index;
	mat3 curWeight = a_weight;
	
	for (int y = 0; y < 3; y++){
		for( int x = 0; x < 3; x++ ){
			float weight = curWeight[x][y];
			if(weight > 0.0){
				int idx = int(curIndex[x][y]);
				
				// transform the offset by bone i
				mat4 m44 = u_boneMatrices[idx];
				transformedPosition += m44 * a_position * weight;
				
				// transform normal by bone i
				mat3 m33 = mat3( m44[0].xyz, m44[1].xyz, m44[2].xyz );
				transformedNormal.xyz += m33 * a_normal.xyz * weight;
			}
		}
	}
				
	vec4 modelCameraPosition = u_modelViewMatrix * transformedPosition;
	lightDir = vec3(u_lightPosition.xyz - modelCameraPosition.xyz);
	viewDir = vec3(-modelCameraPosition.xyz);
	
	gl_Position = u_modelViewProjectionMatrix * transformedPosition;    
	
	normal = u_normalMatrix * transformedNormal.xyz;
}

#else
#ifdef SKIN_POSITIONS_NORMALS_AND_TANGENTS

void skinPositionsNormalsAndTangents()
{
	vec4 transformedPosition = vec4(0.0,0.0,0.0,0.0);
	vec3 transformedNormal = vec3(0.0,0.0,0.0);
	vec3 transformedTangent = vec3(0.0,0.0,0.0);
	
	mat3 curIndex = a_index;
	mat3 curWeight = a_weight;
	
	for (int y = 0; y < 3; y++){
		for( int x = 0; x < 3; x++ ){
			float weight = curWeight[x][y];
			if(weight > 0.0){
				int idx = int(curIndex[x][y]);
				
				// transform the offset by bone i
				mat4 m44 = u_boneMatrices[idx];
				transformedPosition += m44 * a_position * weight;
				
				// transform normal by bone i
				mat3 m33 = mat3( m44[0].xyz, m44[1].xyz, m44[2].xyz );
				transformedNormal += m33 * a_normal.xyz * weight;
				transformedTangent += m33 * a_tangent.xyz * weight;
			}
		}
	}
				
	vec4 modelCameraPosition = u_modelViewMatrix * transformedPosition;
	lightDir = vec3( u_lightPosition.xyz - modelCameraPosition.xyz );
	viewDir = vec3(-modelCameraPosition.xyz );
	
	gl_Position = u_modelViewProjectionMatrix * transformedPosition;    
	
	normal = u_normalMatrix * transformedNormal;
	tangent = u_normalMatrix * transformedTangent;
	
	if( a_tangent.w < 0.0 ){
		binormal = cross(normal, tangent);
	}else{
		binormal = cross(tangent, normal);
	}
}


#endif
#endif
#endif
