//
// Copyright 2010-2011 Fabric Technologies Inc. All rights reserved.
//

use Xfo, Mat33, Mat44;
use OGLShaderProgram;

function loadModelProjectionMatricesFn(
  io OGLShaderProgram shaderProgram,
  Xfo modelXfo,
  Mat44 cameraMatrix,
  Mat44 projectionMatrix
) {
//  report("loadModelProjectionMatrices");
  Integer location;

  location = shaderProgram.getUniformLocation(MODELMATRIX_ATTRIBUTE_ID);
  if(location!=-1){
    shaderProgram.loadMat44Uniform(location, modelXfo.toMat44());
  }
  location = shaderProgram.getUniformLocation(MODELMATRIXINVERSE_ATTRIBUTE_ID);
  if(location!=-1){
    shaderProgram.loadMat44Uniform(location, modelXfo.toMat44().inverse());
  }
  location = shaderProgram.getUniformLocation(VIEWMATRIX_ATTRIBUTE_ID);
  if(location!=-1){
    shaderProgram.loadMat44Uniform(location, cameraMatrix);
  }
  location = shaderProgram.getUniformLocation(CAMERAMATRIX_ATTRIBUTE_ID);
  if(location!=-1){
    // Some shaders need the world space transform of the camera.
    // The camera matrix is inverted. 
    shaderProgram.loadMat44Uniform(location, cameraMatrix.inverse());
  }
  location = shaderProgram.getUniformLocation(CAMERAPOS_ATTRIBUTE_ID);
  if(location!=-1){
    shaderProgram.loadVec3Uniform(location, cameraMatrix.inverse().translation());
  }
  location = shaderProgram.getUniformLocation(PROJECTIONMATRIX_ATTRIBUTE_ID);
  if(location!=-1){
    shaderProgram.loadMat44Uniform(location, projectionMatrix);
  }
  location = shaderProgram.getUniformLocation(NORMALMATRIX_ATTRIBUTE_ID);
  if(location!=-1){
    Mat44 modelViewMatrix = cameraMatrix * modelXfo.toMat44();
    Mat33 normalMatrix = modelViewMatrix.upperLeft().inverse().transpose();
    shaderProgram.loadMat33Uniform(location, normalMatrix);
  }
  location = shaderProgram.getUniformLocation(PROJECTIONMATRIXINV_ATTRIBUTE_ID);
  if(location!=-1){
    shaderProgram.loadMat44Uniform(location, projectionMatrix.inverse());
  }
  location = shaderProgram.getUniformLocation(MODELVIEW_MATRIX_ATTRIBUTE_ID);
  if(location!=-1){
    Mat44 modelViewMatrix = cameraMatrix * modelXfo.toMat44();
    shaderProgram.loadMat44Uniform(location, modelViewMatrix);
  }
  location = shaderProgram.getUniformLocation(MODELVIEWPROJECTION_MATRIX_ATTRIBUTE_ID);
  if(location!=-1){
    Mat44 modelViewProjectionMatrix = projectionMatrix * cameraMatrix * modelXfo.toMat44();
    shaderProgram.loadMat44Uniform(location, modelViewProjectionMatrix);
  }
}

operator loadModelProjectionMatrices(
  io OGLShaderProgram shaderProgram,
  io Xfo modelXfo,
  io Mat44 cameraMatrix,
  io Mat44 projectionMatrix
) {
  loadModelProjectionMatricesFn(
    shaderProgram,
    modelXfo,
    cameraMatrix,
    projectionMatrix
  );
}
operator loadIndexedModelProjectionMatrices(
  io OGLShaderProgram shaderProgram,
  io Xfo modelXfos<>,
  io Size modelXfoIndex,
  io Mat44 cameraMatrix,
  io Mat44 projectionMatrix
) {
  loadModelProjectionMatricesFn(
    shaderProgram,
    modelXfos[modelXfoIndex],
    cameraMatrix,
    projectionMatrix
  );
}