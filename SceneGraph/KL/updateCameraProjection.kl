//
// Copyright 2010-2011 Fabric Technologies Inc. All rights reserved.
//

use Mat44, Vec2;

function Mat44 asymmetricPerspectiveFrustum(
  Scalar l, Scalar r,
  Scalar b, Scalar t,
  Scalar n, Scalar f
) {
  var Scalar w = r - l;
  var Scalar h = t - b;
  var Scalar d = f - n;

  var Mat44 result(
    2.0 * n / w, 0.0, 0.0, 0.0,
    0.0, 2.0 * n / h, 0.0, 0.0,
    (r + l) / w, (t + b) / h, -(f + n) / d, - 1.0,
  0.0, 0.0, -(2.0 * f * n) / d, 0.0);
  return result;
}


// Taken from http://www.geeks3d.com/20090729/howto-perspective-projection-matrix-in-opengl/
function Mat44 symmetricPerspectiveFrustum(
  Scalar width, Scalar height,
  Scalar near, Scalar far,
  Scalar centerX, Scalar centerY
) {
  var Scalar d = far - near;
  var Scalar q = -(far + near) / d;
  var Scalar qn = -2.0 * (far * near) / d;
  var Scalar w = 2.0 * (near / width);
  var Scalar h = 2.0 * (near / height);
  var Mat44 result(
    w, 0.0, -centerX, 0.0,
    0.0, h, -centerY, 0.0,
    0.0, 0.0, q, qn,
    0.0, 0.0, -1.0, 0.0);
  return result;
}

function Mat44 asymmetricOrthographicFrustum(
  Scalar l, Scalar r,
  Scalar b, Scalar t,
  Scalar n, Scalar f
) {
  var Scalar w = r - l;
  var Scalar h = t - b;
  var Scalar d = f - n;

  var Mat44 result(
    2.0 / w, 0.0, 0.0, 0.0,
    0.0, 2.0 / h, 0.0, 0.0,
    0.0, 0.0, - 2.0 / d, 0.0,
  -(r + l) / w, -(t + b) / h, -(f + n) / d, 1.0);
  return result;
}

function Mat44 symmetricOrthographicFrustum(
  Scalar w, Scalar h,
  Scalar n, Scalar f
) {
  var Scalar d = f - n;
  var Mat44 result(
    2.0 / w, 0.0, 0.0, 0.0,
    0.0, 2.0 / h, 0.0, 0.0,
    0.0, 0.0, - 2.0 / d, 0.0,
  0.0, 0.0, -(f + n) / d, 1.0);
  return result;
}

operator updateCameraProjection(
  io Mat44 projectionMat44,
  io Integer width,
  io Integer height,
  io Scalar near,
  io Scalar far,
  io Scalar fovY,
  io Boolean orthographic,
  io Vec2 screenOffset
) {
//  report( "updateCameraProjection\n" );

  if (width < 1) width = 1;
  if (height < 1) height = 1;

  var Scalar aspectRatio = Scalar(width) / Scalar(height);

  if (!orthographic) {
    var Scalar frustumH = (tan(fovY / 2) * near) * 2.0;
    var Scalar frustumW = frustumH * aspectRatio;
    projectionMat44 = symmetricPerspectiveFrustum(frustumW, frustumH, near, far, screenOffset.x, screenOffset.y);
  }
  else {
    var Scalar frustumH = (tan(fovY / 2) * near) * 30.0; // [PT] this value was 150.0, but I have no idea why. It made my orthoraphic camers zoom right out. I changed it for the Google demo.
    var Scalar frustumW = frustumH * aspectRatio;
    projectionMat44 = symmetricOrthographicFrustum(frustumW, frustumH, near, far);
  }
}