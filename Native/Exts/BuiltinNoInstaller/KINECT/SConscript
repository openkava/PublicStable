#
# Copyright 2010-2011 Fabric Technologies Inc. All rights reserved.
#

Import( 'extsEnv', 'fabricExtsDistDir', 'ExtSLName', 'buildOS', 'buildArch', 'FABRIC_VERSION', 'distDir', 'generateDistZip', 'agplLicenseFile' )

import os
import os.path

# only do this for windows
if buildOS != 'Windows':
    Return()

kinectPackageName = "FabricEngine-KinectExt-"+buildOS+"-"+buildArch+"-"+FABRIC_VERSION
kinectPackageDir = distDir.Dir(kinectPackageName)
    
kinectPluginEnv = extsEnv.Clone()

# find the kinect sdk
kinectInclude = 'C:\\Program Files\\Microsoft SDKs\Kinect\\v1.0 Beta2\\inc'
if not os.path.exists(kinectInclude):
    print('WARNING: Skipping build of KINECT extension, no KINECT Include Folder found!')
    Return()

kinectLib = 'C:\\Program Files\\Microsoft SDKs\\Kinect\\v1.0 Beta2\\lib\\x86\\MSRKinectNUI.lib'
if not os.path.exists(kinectLib):
    print('WARNING: Skipping build of KINECT extension, no KINECT Object Library found!')
    Return()

kinectDllPath = 'C:\\Windows\\system32\\MSRKINECTNUI.DLL'
if not os.path.exists(kinectDllPath):
    print('WARNING: Skipping build of KINECT extension, no KINECT DLL found!')
    Return()
    
kinectPluginEnv.Append(CPPPATH = [kinectInclude])
kinectPluginEnv.Append(LIBS = [kinectLib])
kinectSources = Glob('*.cpp')
kinectPlugin = kinectPluginEnv.SharedLibrary( ExtSLName('FabricKINECT'), kinectSources ),
kinectManifest = kinectPluginEnv.File('FabricKINECT.fpm.json'),
kinectKL = kinectPluginEnv.File('FabricKINECT.kl'),
kinectDLL = kinectPluginEnv.File(kinectDllPath),
kinectFiles = [kinectManifest, kinectPlugin, kinectKL, kinectDLL]

kinectInstallerFiles = [
  Install(
    kinectPackageDir,
    [kinectPluginEnv.File("README.txt"), agplLicenseFile]
  ),
  Install(
    kinectPackageDir.Dir('Exts'),
    kinectFiles
  )
  ]

installerEnv = kinectPluginEnv.Clone()
generateDistZip( installerEnv )
installerEnv.Append( DISTZIP_ROOTDIR = kinectPackageDir )
kinectInstaller = installerEnv.DistZip(
  distDir.File(kinectPackageName+".zip"),
  kinectInstallerFiles
)
Alias( 'kinectInstaller', kinectInstaller )
Export( 'kinectInstaller' )

Return( 'kinectFiles' )
