#
# Copyright 2010-2011 Fabric Engine Inc. All rights reserved.
#

import os, platform, posixpath, glob, sys
Import('env', 'buildDir', 'distDir')

try:
  buildOS = os.environ['FABRIC_BUILD_OS']
  Export( 'buildOS' );
  buildArch = os.environ['FABRIC_BUILD_ARCH']
  Export( 'buildArch' )
  buildType = os.environ['FABRIC_BUILD_TYPE']
  Export( 'buildType' )
except:
  print "Must source fabric-build-env.sh first"
  sys.exit(1)

path = os.environ['PATH']+os.pathsep+os.path.join('Native', 'ThirdParty', 'bin', buildOS)
nativeEnv = env.Clone(
  ENV={ 'PATH': path },
  FABRIC_BUILD_OS=buildOS,
  FABRIC_BUILD_ARCH=buildArch,
  FABRIC_BUILD_TYPE=buildType,
  TARGET_ARCH = buildArch
  )

# [pzion 20110726] !!!!!!!!!!!!!!!!!!!!
# When you change the version number,
# don't forget to consider the expiry
# and cache generation!!
# !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
FABRIC_NAME='Fabric Engine'
FABRIC_COMPANY_NAME='Fabric Engine Inc.'
FABRIC_DESC='High Performance Computing for the Web'
FABRIC_COPYRIGHT='Copyright 2010-2012 ' + FABRIC_COMPANY_NAME + '  All rights reserved.'
FABRIC_URL="http://fabric-engine.com"
FABRIC_VERSION_MAJ='1'
FABRIC_VERSION_MIN='0'
FABRIC_VERSION_REV='22'
FABRIC_VERSION_SUFFIX='-release'
FABRIC_CACHE_GENERATION='19'
# !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

FABRIC_BASE_VERSION = FABRIC_VERSION_MAJ + '.' + FABRIC_VERSION_MIN + '.' + FABRIC_VERSION_REV
Export( 'FABRIC_BASE_VERSION' )
FABRIC_VERSION = FABRIC_BASE_VERSION + FABRIC_VERSION_SUFFIX
Export( 'FABRIC_VERSION' )

template = Builder( action = "\
  sed \
    -e 's/{{FABRIC_OS}}/"+buildOS+"/g' \
    -e 's/{{FABRIC_ARCH}}/"+buildArch+"/g' \
    -e 's/{{FABRIC_COMPANY_NAME}}/"+FABRIC_COMPANY_NAME+"/g' \
    -e 's/{{FABRIC_NAME}}/"+FABRIC_NAME+"/g' \
    -e 's/{{FABRIC_DESC}}/"+FABRIC_DESC+"/g' \
    -e 's/{{FABRIC_COPYRIGHT}}/"+FABRIC_COPYRIGHT+"/g' \
    -e 's,{{FABRIC_URL}},"+FABRIC_URL+",g' \
    -e 's/{{FABRIC_VERSION_MAJ}}/"+FABRIC_VERSION_MAJ+"/g' \
    -e 's/{{FABRIC_VERSION_MIN}}/"+FABRIC_VERSION_MIN+"/g' \
    -e 's/{{FABRIC_VERSION_REV}}/"+FABRIC_VERSION_REV+"/g' \
    -e 's/{{FABRIC_VERSION_SUFFIX}}/"+FABRIC_VERSION_SUFFIX+"/g' \
    -e 's/{{FABRIC_VERSION}}/"+FABRIC_VERSION+"/g' \
    -e 's/{{FABRIC_CACHE_GENERATION}}/"+FABRIC_CACHE_GENERATION+"/g' \
  <$SOURCE >$TARGET \
" )
nativeEnv.Append( BUILDERS = { 'Template': template } )

if buildOS == 'Windows':
  env['ENV']['TMP'] = os.environ['TMP']

if buildOS == 'Darwin':
  #env.Replace( CC = 'gcc-4.2' )
  #env.Replace( CXX = 'g++-4.2' )
  nativeEnv.Replace( YACC = '/opt/local/bin/bison' )

if not ARGUMENTS.get( 'VERBOSE' ):
	nativeEnv['CCCOMSTR']       = 'Compiling       $SOURCE'
	nativeEnv['CXXCOMSTR']      = 'Compiling       $SOURCE'
	nativeEnv['SHCCCOMSTR']     = 'Compiling       $SOURCE'
	nativeEnv['SHCXXCOMSTR']    = 'Compiling       $SOURCE'
	nativeEnv['YACCCOMSTR']     = 'Bison           $SOURCE'
	nativeEnv['ARCOMSTR']       = 'StaticLibrary   $TARGET'
	nativeEnv['SHLINKCOMSTR']   = 'SharedLibrary   $TARGET'
	nativeEnv['RANLIBCOMSTR']   = 'Ranlib          $TARGET'
	nativeEnv['LINKCOMSTR']     = 'Linking         $TARGET'
	nativeEnv['LDMODULECOMSTR'] = 'LoadableModule  $TARGET'

if buildType == 'Release':
  nativeEnv.Append( CPPDEFINES = ['NDEBUG'] )

# Uncomment the following to have RC leaks logged
# env.Append( CPPDEFINES = ['FABRIC_RC_LEAK_REPORT'] )

if buildOS == 'Windows' and buildType == 'Debug':
  nativeEnv.Append( PDB='${TARGET.base}.pdb' )

# check for buildOS specific flags
buildFlags = { 
  # === OSX ===
  'Darwin': {
    'ccflags': [ '-Wall', '-fvisibility=hidden', '-isysroot', '/Developer/SDKs/MacOSX10.6.sdk' ],
    'linkflags' : [ '-w', '-isysroot', '/Developer/SDKs/MacOSX10.6.sdk' ],
    'universal' : {
      'ccflags': [ '-arch', 'i386', '-arch', 'x86_64' ],
      'linkflags': [ '-arch', 'i386', '-arch', 'x86_64' ]
      },
    'Debug' : {
      'ccflags': [ '-g' ]
      },
    'Release' : {
      'ccflags': [ '-O2' ]
      },
    }, 
  # === LINUX ===
  'Linux': {
    'ccDefs': [
      '__STDC_LIMIT_MACROS',
      '__STDC_CONSTANT_MACROS'
    ],
    'ccflags': ['-fPIC'],
    'i686' : {
      'ccflags': [ '-m32' ],
      'linkflags': [ '-m32' ]
      },
    'x86_64' : {
      'ccflags': [ '-m64' ],
      'linkflags': [ '-m64' ]
      },
    'Debug' : {
      'ccflags': [ '-g' ]
      },
    'Release' : {
      'ccflags': [ '-O2' ]
      },
    }, 
  # === WINDOWS ===
  'Windows': {
    'ccDefs': [ 
      '_SCL_SECURE_NO_WARNINGS=1',
      '_ITERATOR_DEBUG_LEVEL=0',
      '_SECURE_SCL=0',
      '_WIN32_WINNT=0x0501'
    ],
    'ccflags': [ 
      '/EHsc', 
      '/wd4624'    ],
    'linkflags': [ 'chkstk.obj' ],
    'x86' : {
      'ccflags': [ ],
      'linkflags': [ ]
      },
    'AMD64' : {
      'ccflags': [ ],
      'linkflags': [ ]
      },
    'Debug' : {
      'ccflags': [ '/Od', '/Z7', '/MTd' ]
      },
    'Release' : {
      'ccflags': [ '/Ox', '/MT' ]
      },
    }
  }

osFlags = buildFlags[buildOS]

if 'ccDefs' in osFlags:
  nativeEnv.Append( CPPDEFINES = osFlags['ccDefs'] )  
if 'ccflags' in osFlags:
  nativeEnv.Append( CCFLAGS = osFlags['ccflags'] )
if 'cxxflags' in osFlags:
  nativeEnv.Append( CXXFLAGS = osFlags['cxxflags'] )
if 'linkflags' in osFlags:
  nativeEnv.Append( LINKFLAGS = osFlags['linkflags'] )

subTypes = [ buildArch, buildType ]

for subType in subTypes:
  if subType in osFlags:
    osSubFlags = osFlags[subType]
    if 'ccDefs' in osSubFlags:
      nativeEnv.Append( CPPDEFINES = osSubFlags['ccDefs'] )  
    if 'ccflags' in osSubFlags:
      nativeEnv.Append( CCFLAGS = osSubFlags['ccflags'] )
    if 'cxxflags' in osSubFlags:
      nativeEnv.Append( CXXFLAGS = osSubFlags['cxxflags'] )
    if 'linkflags' in osSubFlags:
      nativeEnv.Append( LINKFLAGS = osSubFlags['linkflags'] )

archBuildDirBase = os.path.join(buildDir, 'Native', buildOS, buildArch, buildType)
nativeEnv.Append( CPPPATH=[archBuildDirBase] )
archBuildDir = os.path.join(archBuildDirBase, 'Fabric')
Export('archBuildDir')

archDistDir = distDir.Dir('Native').Dir(buildOS).Dir(buildArch).Dir(buildType)
npapiDistDir = archDistDir.Dir( 'NPAPI' )
fabricExtsDistDir = archDistDir.Dir( 'Exts' )
Export('archDistDir', 'npapiDistDir', 'fabricExtsDistDir')

Export('nativeEnv')

subDirs = [
  'Licenses',
  'ThirdParty',
  'Base',
  'Core',
  'Tools',
  'Clients',
  'EDK',
  'Exts',
  'Installer'
  ]
for subDir in subDirs:
  SConscript( dirs=[subDir], variant_dir=os.path.join(archBuildDir, subDir) )
