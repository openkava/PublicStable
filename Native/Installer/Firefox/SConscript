#
# Copyright 2010-2011 Fabric Technologies Inc. All rights reserved.
#

# Note that the installer with the .xpi extension should be served
# with the MIME type application/x-xpinstall

import os
Import( 'baseEnv', 'buildOS', 'buildArch', 'archDistDir', 'fabricExtsDistDir', 'npapiDistDir', 'installerName', 'generateDistZip', 'extFiles', 'distDir' )

firefoxExtensionDir = archDistDir.Dir( 'FirefoxExtension' )

installerFiles = []

installerFiles.append( baseEnv.Command(
  firefoxExtensionDir.File('chrome.manifest'),
  'chrome.manifest',
  [ Copy( '$TARGET', '$SOURCE' ) ]
) )

baseEnv.Template( "install.rdf", "install.rdf."+buildOS+"."+buildArch+".template" )
installerFiles.append( baseEnv.Command(
  firefoxExtensionDir.File('install.rdf'),
  'install.rdf',
  [ Copy( '$TARGET', '$SOURCE' ) ]
) )

for res in ["48", "64"]:
  installerFiles.append( baseEnv.Command(
    firefoxExtensionDir.Dir('content').File('fabric-engine_'+res+'.png'),
    baseEnv.Dir('content').File('fabric-engine_'+res+'.png'),
    [ Copy( '$TARGET', '$SOURCE' ) ]
  ) )

if buildOS == 'Darwin':
  npapiPluginBundleDir = npapiDistDir.Dir( 'Fabric.'+buildArch+'.plugin' )
  pluginDir = firefoxExtensionDir.Dir('plugins').Dir( 'Fabric.'+buildArch+'.plugin' )
  installerFiles.append( baseEnv.Command(
    pluginDir.Dir('Contents').Dir('MacOS').File('FabricNPAPI'),
    npapiPluginBundleDir.Dir('Contents').Dir('MacOS').File('FabricNPAPI'),
    Copy( '$TARGET', '$SOURCE' )
  ) )
  installerFiles.append( baseEnv.Command(
    pluginDir.Dir('Contents').File('Info.plist'),
    npapiPluginBundleDir.Dir('Contents').File('Info.plist'),
    Copy( '$TARGET', '$SOURCE' )
  ) )
if buildOS == 'Linux':
  installerFiles.append( baseEnv.Command(
    firefoxExtensionDir.Dir('plugins').File('libFabricPlugin.so'),
    npapiDistDir.File('libFabricPlugin.so'),
    Copy( '$TARGET', '$SOURCE' )
  ) )
if buildOS == 'Windows':
  installerFiles.append( baseEnv.Command(
    firefoxExtensionDir.Dir('plugins').File('npFabricPlugin.dll'),
    npapiDistDir.File('npFabricPlugin.dll'),
    Copy( '$TARGET', '$SOURCE' )
  ) )

installerFiles.append( Install(
  firefoxExtensionDir.Dir('plugins'),
  extFiles
) )

installerEnv = baseEnv.Clone()
generateDistZip( installerEnv )
installerEnv.Append( DISTZIP_ROOTDIR = firefoxExtensionDir )
installerFile = distDir.File(installerName + ".xpi")
installerEnv.DistZip(
  installerFile,
  installerFiles
)
updateFile = distDir.File("xpi-update-"+buildOS+"-"+buildArch+".rdf")
baseEnv.Template( updateFile, "xpi-update.rdf.template" )
print( "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!")
print( "!! Firefox Installer: you MUST perform the following steps before uploading:")
print( "!! 1. Calculate the SHA1 hash of "+installerFile.abspath+" and insert it into "+updateFile.abspath )
print( "!! 2. Sign "+updateFile.abspath+" using the McCoy tool" )
print( "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!")
Alias( 'firefoxInstaller', [installerFile, updateFile] )
